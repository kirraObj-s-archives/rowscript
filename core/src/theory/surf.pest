WHITESPACE = _{ WHITE_SPACE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

pub_camelback_id = @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA | ASCII_DIGIT)* }
priv_camelback_id = @{ "_" ~ pub_camelback_id }

pub_camel_id = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHA | ASCII_DIGIT)* }
priv_camel_id = @{ "_" ~ pub_camel_id }

fn_id = { pub_camelback_id | priv_camelback_id }
implicit_id = { pub_camel_id }
row_id = @{ "'" ~ pub_camel_id }
field_id = { pub_camelback_id | priv_camelback_id }
param_id = { pub_camelback_id }
let_id = { pub_camelback_id }
idref =
  { fn_id
  | implicit_id
  | row_id
  | param_id
  | let_id
  }

hole = { "?" }

string = @{ "\"" ~ (unescaped_string_fragment | escaped_string_fragment)* ~ "\"" }
unescaped_string_fragment = @{ (!("\"" | "\\") ~ ANY)+ }
escaped_string_fragment =
 @{ "\\"
  ~ ( !("x" | "u" | ASCII_OCT_DIGIT)
    | ASCII_OCT_DIGIT{1,3}
    | "x" ~ ASCII_HEX_DIGIT{2}
    | "u" ~ ASCII_HEX_DIGIT{4}
    | "u{" ~ ASCII_HEX_DIGIT+ ~ "}"
    )
  }

number =
  { binary_number
  | octal_number
  | hex_number
  | decimal_number
  }
bigint = @{ (binary_number | octal_number | hex_number | decimal_digits) ~ "n" }
binary_number = @{ ("0b" | "0B") ~ ASCII_BIN_DIGIT ~ ("_"? ~ ASCII_BIN_DIGIT)* }
octal_number = @{ ("0o" | "0O") ~ ASCII_OCT_DIGIT ~ ("_"? ~ ASCII_OCT_DIGIT)* }
hex_number = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT ~ ("_"? ~ ASCII_HEX_DIGIT)* }
decimal_number =
 @{ decimal_digits
  | decimal_integer ~ exponent_part
  | "." ~ decimal_digits ~ (exponent_part)?
  | decimal_integer ~ "." ~ decimal_digits? ~ exponent_part?
  }
decimal_digits = @{ ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)* }
exponent_part = @{ ("e" | "E")? ~ ("-" | "+")? ~ decimal_digits }
decimal_integer = @{ "0" | "0"? ~ ASCII_NONZERO_DIGIT ~ ("_"? ~ decimal_digits)? }

implicit_ids = _{ implicit_id ~ ("," ~ implicit_id)* }
row_ids = _{ row_id ~ ("," ~ row_id)* }
implicits =
 _{ "<" ~ implicit_ids ~ ">"
  | "<" ~ row_ids ~ ">"
  | "<" ~ row_ids ~ "," ~ implicit_ids ~ ">"
  }

param = { param_id ~ ":" ~ type_expr }
params = _{ "()" | "(" ~ param ~ ("," ~ param)* ~ ")" }

field = { field_id ~ ":" ~ type_expr }

type_expr =
  { fn_type
  | string_type
  | number_type
  | bigint_type
  | boolean_type
  | unit_type
  | object_type
  | idref
  | paren_type_expr
  | hole
  }
paren_type_expr = { "(" ~ type_expr ~ ")" }
fn_type = { params ~ "->" ~ type_expr }
string_type = { "string" }
number_type = { "number" }
bigint_type = { "bigint" }
boolean_type = { "boolean" }
unit_type = { "unit" }
object_type = { object_ref | simple_object }
object_ref = { "{" ~ row_id ~ "}" }
simple_object = { "{" ~ field ~ ("," ~ field)* ~ "}" }

primary_expr =
  { string
  | number
  | bigint
  | boolean_false
  | boolean_true
  | boolean_if
  | lambda_expr
  | app
  | tt
  | idref
  | paren_primary_expr
  | hole
  }
paren_primary_expr = { "(" ~ primary_expr ~ ")" }
lambda_expr = { lambda_params ~ "=>" ~ lambda_body }
lambda_params = _{ "()" | param_id | "(" ~ param_id ~ ("," ~ param_id)+ ~ ")" }
lambda_body = { primary_expr | fn_body_block }
app = { ("(" ~ primary_expr ~ ")" | idref) ~ type_args? ~ args+ }
type_args = _{ "<" ~ type_expr ~ ("," ~ type_expr)* ~ ">" }
args = { "()" | "(" ~ primary_expr ~ ("," ~ primary_expr)* ~ ")" }
boolean_false = { "false" }
boolean_true = { "true" }
boolean_if =
  { "if" ~ "(" ~ primary_expr ~ ")"
  ~ "{" ~ branch ~ "}"
  ~ "else" ~ "{" ~ branch ~ "}"
  }
branch = { branch_let | primary_expr }
branch_let = { "let" ~ param_id ~ (":" ~ type_expr)? ~ "=" ~ primary_expr ~ ";" ~ branch }
tt = { "()" }

row_pred =
  { row_ord
  | row_eq
  }
row_ord = { row_expr ~ (row_le | row_ge) ~ row_expr }
row_le = { "<:" }
row_ge = { ":>" }
row_eq = { row_expr ~ "=" ~ row_expr }
row_expr =
  { row_concat
  | row_primary_expr
  }
row_primary_expr =
  { idref
  | paren_fields
  | paren_row_expr
  }
paren_fields = { "(" ~ field ~ ("," ~ field)* ~ ")" }
row_concat = { row_primary_expr ~ "+" ~ row_expr }
paren_row_expr = { "(" ~ row_expr ~ ")" }

fn_def =
  { "function"
  ~ fn_id
  ~ implicits?
  ~ params
  ~ (":" ~ type_expr)?
  ~ ("where" ~ row_pred ~ ("and" ~ row_pred)*)?
  ~ fn_body_block
  }
fn_body_block = _{ "{" ~ fn_body ~ "}" }
fn_body = { fn_body_let | fn_body_ret }
fn_body_let = { "let" ~ param_id ~ (":" ~ type_expr)? ~ "=" ~ primary_expr ~ ";" ~ fn_body }
fn_body_ret = { "return" ~ primary_expr? }

fn_postulate =
  { "function"
  ~ fn_id
  ~ params
  ~ (":" ~ type_expr)?
  ~ ";"
  }

decl =
 _{ fn_def
  | fn_postulate
  }

file = { SOI ~ decl* ~ EOI }
