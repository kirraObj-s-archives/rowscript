WHITESPACE = _{ WHITE_SPACE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

pub_camelback_id = @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHA | ASCII_DIGIT)* }
priv_camelback_id = @{ "_" ~ pub_camelback_id }

pub_camel_id = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHA | ASCII_DIGIT)* }
priv_camel_id = @{ "_" ~ pub_camel_id }

fn_id = { pub_camelback_id | priv_camelback_id }
row_id = @{ "'" ~ pub_camel_id }
implicit_id = { pub_camel_id }
field_id = { pub_camelback_id | priv_camelback_id }
param_id = { pub_camelback_id }
let_id = { pub_camelback_id }
idref =
  { fn_id
  | implicit_id
  | param_id
  | let_id
  }

hole = { "?" }

string = @{ "\"" ~ (unescaped_string_fragment | escaped_string_fragment)* ~ "\"" }
unescaped_string_fragment = @{ (!("\"" | "\\") ~ ANY)+ }
escaped_string_fragment =
 @{ "\\"
  ~ ( !("x" | "u" | ASCII_OCT_DIGIT)
    | ASCII_OCT_DIGIT{1,3}
    | "x" ~ ASCII_HEX_DIGIT{2}
    | "u" ~ ASCII_HEX_DIGIT{4}
    | "u{" ~ ASCII_HEX_DIGIT+ ~ "}"
    )
  }

number =
  { binary_number
  | octal_number
  | hex_number
  | decimal_number
  }
bigint = @{ (binary_number | octal_number | hex_number | decimal_digits) ~ "n" }
binary_number = @{ ("0b" | "0B") ~ ASCII_BIN_DIGIT ~ ("_"? ~ ASCII_BIN_DIGIT)* }
octal_number = @{ ("0o" | "0O") ~ ASCII_OCT_DIGIT ~ ("_"? ~ ASCII_OCT_DIGIT)* }
hex_number = @{ ("0x" | "0X") ~ ASCII_HEX_DIGIT ~ ("_"? ~ ASCII_HEX_DIGIT)* }
decimal_number =
 @{ decimal_digits
  | decimal_integer ~ exponent_part
  | "." ~ decimal_digits ~ (exponent_part)?
  | decimal_integer ~ "." ~ decimal_digits? ~ exponent_part?
  }
decimal_digits = @{ ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)* }
exponent_part = @{ ("e" | "E")? ~ ("-" | "+")? ~ decimal_digits }
decimal_integer = @{ "0" | "0"? ~ ASCII_NONZERO_DIGIT ~ ("_"? ~ decimal_digits)? }

implicit_ids = _{ implicit_id ~ ("," ~ implicit_id)* }
row_ids = _{ row_id ~ ("," ~ row_id)* }
implicits =
 _{ "<" ~ row_ids ~ ">"
  | "<" ~ implicit_ids ~ ">"
  | "<" ~ row_ids ~ "," ~ implicit_ids ~ ">"
  }

param = { param_id ~ ":" ~ type_expr }
params = _{ "()" | "(" ~ param ~ ("," ~ param)* ~ ")" }

field = { field_id ~ ":" ~ type_expr }
label = { field_id ~ ":" ~ expr }

type_expr =
  { fn_type
  | string_type
  | number_type
  | bigint_type
  | boolean_type
  | unit_type
  | object_type
  | idref
  | paren_type_expr
  | hole
  | row_id
  }
paren_type_expr = { "(" ~ type_expr ~ ")" }
fn_type = { params ~ "->" ~ type_expr }
string_type = { "string" }
number_type = { "number" }
bigint_type = { "bigint" }
boolean_type = { "boolean" }
unit_type = { "unit" }
object_type = { object_ref | object_type_literal }
object_ref = { "{" ~ row_id ~ "}" }
object_type_literal = { "{" ~ field ~ ("," ~ field)* ~ "}" }

expr =
  { string
  | number
  | bigint
  | boolean_false
  | boolean_true
  | boolean_if
  | lambda_expr
  | app
  | tt
  | labels
  | idref
  | paren_expr
  | hole
  }
boolean_false = { "false" }
boolean_true = { "true" }
boolean_if =
  { "if" ~ "(" ~ expr ~ ")"
  ~ "{" ~ branch ~ "}"
  ~ "else" ~ "{" ~ branch ~ "}"
  }
branch = { branch_let | expr }
branch_let = { "let" ~ param_id ~ (":" ~ type_expr)? ~ "=" ~ expr ~ ";" ~ branch }
lambda_expr = { lambda_params ~ "=>" ~ lambda_body }
lambda_params = _{ "()" | param_id | "(" ~ param_id ~ ("," ~ param_id)+ ~ ")" }
lambda_body = { expr | fn_body_block }
app = { ("(" ~ expr ~ ")" | idref) ~ implicit_args? ~ args+ }
implicit_args =
 _{ "<" ~ row_args ~ ">"
  | "<" ~ type_args ~ ">"
  | "<" ~ row_args ~ "," ~ type_args ~ ">"
  }
type_args = _{ type_arg ~ ("," ~ type_arg)* }
type_arg = { (idref ~ "=")? ~ type_expr }
row_args = _{ row_arg ~ ("," ~ row_arg)* }
row_arg = { (idref ~ "=")? ~ paren_fields }
args = { "()" | "(" ~ expr ~ ("," ~ expr)* ~ ")" }
tt = { "()" }
labels = { "{" ~ label ~ ("," ~ label)* ~ "}" }
paren_expr = { "(" ~ expr ~ ")" }

row_pred =
  { row_ord
  | row_eq
  }
row_ord = { row_expr ~ (row_le | row_ge) ~ row_expr }
row_le = { "<:" }
row_ge = { ":>" }
row_eq = { row_expr ~ "=" ~ row_expr }
row_expr =
  { row_concat
  | row_primary_expr
  }
row_primary_expr =
  { row_id
  | paren_fields
  | paren_row_expr
  }
paren_fields = { "(" ~ field ~ ("," ~ field)* ~ ")" }
row_concat = { row_primary_expr ~ "+" ~ row_expr }
paren_row_expr = { "(" ~ row_expr ~ ")" }

fn_def =
  { "function"
  ~ fn_id
  ~ implicits?
  ~ params
  ~ (":" ~ type_expr)?
  ~ ("where" ~ row_pred ~ ("," ~ row_pred)*)?
  ~ fn_body_block
  }
fn_body_block = _{ "{" ~ fn_body ~ "}" }
fn_body = { fn_body_let | fn_body_ret }
fn_body_let = { "let" ~ param_id ~ (":" ~ type_expr)? ~ "=" ~ expr ~ ";" ~ fn_body }
fn_body_ret = { "return" ~ expr? }

fn_postulate =
  { "function"
  ~ fn_id
  ~ params
  ~ (":" ~ type_expr)?
  ~ ";"
  }

decl =
 _{ fn_def
  | fn_postulate
  }

file = { SOI ~ decl* ~ EOI }
